/***************************************************
 ** @author   STF 433
 ** @version  0.0.1
 ** @purpose  1:27.7, Ensure that the IUT correctly handles attribute definitions and their scoping rules
 ** @verdict  pass reject
 ***************************************************/

module NegSem_2707_OptionalAttributes_003 {

	type enumerated EnumeratedType {e_black, e_white};

	type record RecordType {
 	 integer  a optional,
 	 integer  b optional,
 	 boolean  c
	}

	type set SetType {
 	 integer  a optional,
 	 integer  b optional,
 	 boolean  c
	}

	type record length (1..2) of integer IntegerList;

	type set length (1..2) of integer IntegerUList;

	type union UnionType {
 	 integer  a,
 	 EnumeratedType  b,
 	 boolean  c
	}

	type record MessageType {
  	 integer  		field1,
  	 charstring  	field2,
  	 boolean  		field3,
  	 integer  		field4[4],
  	 EnumeratedType field5,
  	 RecordType		field6,
  	 SetType		field7,
  	 UnionType		field8,
  	 IntegerList	field9,
  	 IntegerUList	field10
	}

 template MessageType mw_matchingTemplate:=
 {
  field1 := 1,
  field2 := "test string",
  field3 := true,
  field4 := {1,2,3,4},
  field5 := e_black,
  field6 := {b:=3,c:=true}, 	//a is still undefined
  field7 := {a:=3,c:=true}, 	//b is still undefined
  field8 := {a:=1},
  field9 := {1},
  field10 := {1,2}
 } with { optional (field6.a, field7.b) "implicit omit"}

 const MessageType c_testMessage:=  {
  field1 := 1,
  field2 := "test string",
  field3 := true,
  field4 := {1,2,3,4},
  field5 := e_black,
  field6 := {a:=3,b:=3,c:=true},
  field7 := {a:=3,b:=3,c:=true},
  field8 := {a:=1},
  field9 := {1},
  field10 := {1,2}
 }

    type port loopbackPort message {
	  inout MessageType
	}


type component GeneralComp {
	  port loopbackPort messagePort
}

testcase TC_NegSem_2707_OptionalAttributes_003() runs on GeneralComp {

    var MessageType v_testMessage:=c_testMessage;

 messagePort.send(v_testMessage);

    alt {
     [] messagePort.receive(mw_matchingTemplate) {  //cannot match not fully defined template
        setverdict(pass);
     }
     [] messagePort.receive {
        setverdict(fail);
     }
    }
}

control{
    execute(TC_NegSem_2707_OptionalAttributes_003());
}

} with { optional override "explicit omit"}
