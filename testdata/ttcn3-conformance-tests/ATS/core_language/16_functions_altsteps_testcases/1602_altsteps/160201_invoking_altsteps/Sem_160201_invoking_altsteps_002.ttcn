/***************************************************
 ** @author   STF 409
 ** @version  0.0.1
 ** @purpose  1:16.2.1, Ensure that the IUT recognizes altstep definitions and correctly evaluates them
 ** @verdict  pass accept, ttcn3verdict:pass
 ***************************************************/

module Sem_160201_invoking_altsteps_002 {

	type record MessageType {
  	 integer  		field1,
  	 charstring  	field2
	}

    type port loopbackPort message {
	  inout MessageType
	}

	type component GeneralComp {
	  port loopbackPort messagePort
	}


altstep AltSet1() runs on GeneralComp {

     [] messagePort.check {
        setverdict(inconc);
     }

}

altstep AltSet2() runs on GeneralComp {

     [] messagePort.receive {
        setverdict(pass);
     }

}

testcase TC_Sem_160201_invoking_altsteps_002 () runs on GeneralComp {
 var MessageType v_testMessage;
 timer t_timer;
 v_testMessage:=  {
  field1 := 1,
  field2 := "test string"
 }

 messagePort.send(v_testMessage);
 t_timer.start( 1.0 );

 alt {
  [] AltSet2();
  [] AltSet1();		//evaluation of sequential altstep activations
  [] t_timer.timeout {
        setverdict(fail);
     }
 }

}

control{

    execute(TC_Sem_160201_invoking_altsteps_002());

}

}
