/*****************************************************************
 ** @author   STF 470
 ** @version  0.0.1
 ** @purpose  1:22.4, Verify behaviour of any port.check(getcall) with assignment in case of unsuccessful match inside alt
 ** @verdict  pass accept, ttcn3verdict:pass
 *****************************************************************/
module Sem_2204_the_check_operation_038 {

	signature S(integer p_par1);
	type port P procedure {
		inout S;
	}

    type component GeneralComp {
		port P p1, p2;
	}

	function f() runs on GeneralComp {
		var integer v_val;
        alt
		{
        	[] any port.check(getcall(S:{ p_par1 := (0, 2, 4, 6)}) -> param(v_val := p_par1)) {
				setverdict(fail, "Incorrect match");
			}
			[] any port.getcall {
				if (not isbound(v_val)) { setverdict(pass, "As expected, the check operation didn't match"); }
				else { setverdict(fail, "The value should still be undefined at this point"); }
			}
		}
	}

	testcase TC_Sem_2204_the_check_operation_038() runs on GeneralComp system GeneralComp{
		var GeneralComp v_ptc := GeneralComp.create;
		connect(self:p1, v_ptc:p1);
		connect(self:p2, v_ptc:p2);
		p2.call(S:{ p_par1 := 1 }, nowait);
		v_ptc.start(f());
		v_ptc.done;
    }

    control {
        execute(TC_Sem_2204_the_check_operation_038(), 5.0);
    }
}