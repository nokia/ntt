/*****************************************************************
 ** @author   STF 521
 ** @version  0.0.1
 ** @purpose  1:22.3.6, catch with a from clause (single item)
 ** @verdict  pass accept, ttcn3verdict:pass
 *****************************************************************/

// The following requirements are tested:
// A catch operation may be restricted to a certain communication partner in case
// of one-to-many connections. This restriction shall be denoted by using the from
// keyword followed by a specification of an address or component reference, a list
// of address or component references or any component.

module Sem_220306_catch_operation_015 {

	signature S() exception (charstring);

	type port P procedure {
		inout S;
	}

    type component GeneralComp
	{
		port P p;
	}

	function f() runs on GeneralComp
	{
        p.getcall(S:{});
        p.raise(S, "exc");
	}

	const integer c_ptcCount := 3;

    testcase TC_Sem_220306_catch_operation_015() runs on GeneralComp system GeneralComp {
	    var GeneralComp v_ptcs[c_ptcCount];
        var integer v_receiveCounter := 0;
        for (var integer i := 0; i < c_ptcCount; i := i + 1) {
            v_ptcs[i] := GeneralComp.create;
            connect(self:p, v_ptcs[i]:p);
            v_ptcs[i].start(f());
			p.call(S:{}, nowait) to v_ptcs[i];
        }

        for (var integer i := 0; i < c_ptcCount; i := i + 1) {
            alt {
                [] p.catch(S, charstring:?) from v_ptcs[0] { setverdict(pass); } // expected 1 from match
                [] p.catch(S, charstring:?) { v_receiveCounter := v_receiveCounter + 1; } // expected 2 other received exceptions
            }
        }
        if (v_receiveCounter != c_ptcCount - 1) { setverdict(fail); }
    }

    control {
        execute(TC_Sem_220306_catch_operation_015(), 5.0);
    }
}