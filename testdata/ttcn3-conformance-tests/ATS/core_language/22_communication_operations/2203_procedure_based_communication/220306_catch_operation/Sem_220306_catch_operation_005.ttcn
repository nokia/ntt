/*****************************************************************
 ** @author   STF 470
 ** @version  0.0.1
 ** @purpose  1:22.3.6, Verify that any from done assigns index
 ** @verdict  pass accept, ttcn3verdict:pass
 *****************************************************************/
// The following requirements are tested:
// It is also possible to store the index of a port in a single-dimensional port array at which
// the operation was successful to a variable of type integer or, in case of multi-dimensional
// port arrays the index of the successful port to an integer array or record of integer variable.
// When checking the port array for matching exceptions, the port indices to be checked are iterated
// from lowest to highest.
module Sem_220306_catch_operation_005 {

    signature S() exception(integer);

	type port P procedure {
		inout S;
	}

	const integer c_portCount := 4;
    type component GeneralComp
	{
		port P p[c_portCount];
	}

	function f() runs on GeneralComp
	{
		for(var integer i := 0; i < c_portCount; i := i + 1) {
			p[i].getcall;
			if (i mod 2 == 1) { p[i].raise(S, i + 1); };
		}
	}

    testcase TC_Sem_220306_catch_operation_005() runs on GeneralComp system GeneralComp {
		var GeneralComp v_ptc := GeneralComp.create;
		var integer v_index, v_res;
		for(var integer i := 0; i < c_portCount; i := i + 1) {
			connect(self:p[i], v_ptc:p[i]);
			p[i].call(S:{}, nowait);
		}
		v_ptc.start(f());
		v_ptc.done;
        alt
		{
        	[] any from p.catch(S, integer:(0..c_portCount)) -> value v_res @index value v_index {
				if(v_index == 1 and v_res == v_index + 1){
		            setverdict(pass);
		        } else {
		            setverdict(fail, "Index or parameter value incorrectly assigned");
		        }
			}
			[else] { setverdict(fail, "The any from catch operation didn't match for some reason");  }
		}
	}

    control {
        execute(TC_Sem_220306_catch_operation_005(), 5.0);
    }
}