/*****************************************************************
 ** @author   STF 470
 ** @version  0.0.1
 ** @purpose  1:22.3.6, Verify that any from catch index redirection for multi-D arrays requires arrays
 ** @verdict  pass reject
 *****************************************************************/
// The following requirements are tested:
// Restriction j:
// If the index redirection is used for multi-dimensional component arrays, the size
// of the integer array or record of integer type shall exactly be the same as the dimension
// of the respective array, and its type shall allow storing the highest index (from all
// dimensions) of the array.
module NegSem_220306_catch_operation_005 {

    signature S() exception(integer);

	type port P procedure {
		inout S;
	}

	const integer c_portCount := 3;
    type component GeneralComp
	{
		port P p[c_portCount][c_portCount];
	}

	function f() runs on GeneralComp
	{
		for(var integer i := 0; i < c_portCount; i := i + 1) {
			for(var integer j := 0; j < c_portCount; j := j + 1) {
				p[i][j].getcall(S:?);
				if (i == 1 and j == 2 or i == 2 and j == 1) { p[i][j].raise(S, 10); }
			}
		}
	}

    testcase TC_NegSem_220306_catch_operation_005() runs on GeneralComp system GeneralComp {
		var GeneralComp v_ptc := GeneralComp.create, v_src;
		var integer v_index;
		for(var integer i := 0; i < c_portCount; i := i + 1) {
			for(var integer j := 0; j < c_portCount; j := j + 1) {
				connect(self:p[i][j], v_ptc:p[i][j]);
				p[i][j].call(S:{}, nowait);
			}
		}
		v_ptc.start(f());
		v_ptc.done;
        alt
		{
        	[] any from p.catch(S, integer:?) -> @index value v_index {
				if(v_index == 1){
		            setverdict(pass);
		        } else {
		            setverdict(fail, "Indices or parameter value incorrectly assigned");
		        }
			}
			[else] { setverdict(fail, "The any from catch operation didn't match for some reason");  }
		}
	}

    control {
        execute(TC_NegSem_220306_catch_operation_005(), 5.0);
    }
}