/*****************************************************************
 ** @author   STF 521
 ** @version  0.0.1
 ** @purpose  1:22.3.4, getreply with a from clause (single item)
 ** @verdict  pass accept, ttcn3verdict:pass
 *****************************************************************/

// The following requirements are tested:
// A getreply operation may be restricted to a certain communication partner in case
// of one-to-many connections. This restriction shall be denoted by using the from
// keyword followed by a specification of an address or component reference, a list
// of address or component references or any component.

module Sem_220304_getreply_operation_021 {

	signature S() return integer;

	type port P procedure {
		inout S;
	}

    type component GeneralComp {
		port P p;
	}

	function f() runs on GeneralComp {
        p.getcall(S:{});
        p.reply(S:{} value 1);
	}

	const integer c_ptcCount := 3;

    testcase TC_Sem_220304_getreply_operation_021() runs on GeneralComp system GeneralComp {
	    var GeneralComp v_ptcs[c_ptcCount];
        var integer v_receiveCounter := 0;
        for (var integer i := 0; i < c_ptcCount; i := i + 1) {
            v_ptcs[i] := GeneralComp.create;
            connect(self:p, v_ptcs[i]:p);
            v_ptcs[i].start(f());
			p.call(S:{}, nowait) to v_ptcs[i];
        }

        for (var integer i := 0; i < c_ptcCount; i := i + 1) {
            alt {
                [] p.getreply(S:?) from v_ptcs[0] { setverdict(pass); } // expected 1 from match
                [] p.getreply(S:?) { v_receiveCounter := v_receiveCounter + 1; } // expected 2 other received exceptions
            }
        }
        if (v_receiveCounter != c_ptcCount - 1) { setverdict(fail); }
    }

    control {
        execute(TC_Sem_220304_getreply_operation_021(), 5.0);
    }
}