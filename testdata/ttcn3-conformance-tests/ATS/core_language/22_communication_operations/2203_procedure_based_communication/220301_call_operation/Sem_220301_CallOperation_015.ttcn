/*****************************************************************
 ** @author   STF 521
 ** @version  0.0.1
 ** @purpose  1:22.3.1, non-blocking multicast call
 ** @verdict  pass accept, ttcn3verdict:pass
 *****************************************************************/

// The following requirements are tested:
// In case of a multicast or broadcast call operation of a non-blocking procedure, all exceptions which
// may be raised from the different communication partners can be handled in subsequent catch, alt or
// interleave statements.

module Sem_220301_CallOperation_015 {

	signature S() noblock;

	type port P procedure {
		inout S;
	}

    type component GeneralComp {
		port P p;
	}

	function f_called() runs on GeneralComp {
		p.getcall(S:?);
		p.reply(S:{});
	}

    testcase TC_Sem_220301_CallOperation_015() runs on GeneralComp system GeneralComp {
        var GeneralComp v_ptc1 := GeneralComp.create,
			v_ptc2 := GeneralComp.create,
			v_ptc3 := GeneralComp.create,
			v_comp;
		var integer v_counter := 0;
		connect(self:p, v_ptc1:p);
		connect(self:p, v_ptc2:p);
		connect(self:p, v_ptc3:p);
		v_ptc1.start(f_called());
		v_ptc2.start(f_called());
		v_ptc3.start(f_called());
		p.call(S:{}) to (v_ptc1, v_ptc3);
		alt {
			[] p.getreply(S:?) -> sender v_comp {
				if (v_comp == v_ptc1 or v_comp == v_ptc3) {
					v_counter := v_counter + 1;
					if (v_counter < 2) {
						repeat;
					} else {
						setverdict(pass, "Both replies received");  // success
					}
				} else {
					setverdict(fail, "Wrong component");
				}
			}
		}
    }

    control {
        execute(TC_Sem_220301_CallOperation_015(), 5.0);
    }
}