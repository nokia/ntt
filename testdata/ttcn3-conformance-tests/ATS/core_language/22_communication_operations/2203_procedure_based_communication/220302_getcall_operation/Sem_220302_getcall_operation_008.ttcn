/*****************************************************************
 ** @author   STF 470
 ** @version  0.0.1
 ** @purpose  1:22.3.2, Verify that any from getcall doesn't assign index when there's no suitable match
 ** @verdict  pass accept, ttcn3verdict:pass
 *****************************************************************/
// The following requirements are tested:
// It is also possible to store the index of a port in a single-dimensional port array at which
// the operation was successful to a variable of type integer or, in case of multi-dimensional
// port arrays the index of the successful port to an integer array or record of integer variable.
module Sem_220302_getcall_operation_008 {

    signature S(integer p_par);

	type port P procedure {
		inout S;
	}

	const integer c_portCount := 4;
    type component GeneralComp
	{
		port P p[c_portCount];
	}

	function f() runs on GeneralComp
	{
		var integer v_index;
        alt
		{
        	[] any from p.getcall(S:{p_par := (1..10)}) -> @index value v_index {
				setverdict(fail, "The any from getcall operation produced incorrect match");
			}
			[else] { setverdict(pass); }
		}
		if(not isbound(v_index)){
            setverdict(pass);
        } else {
            setverdict(fail, "Index incorrectly assigned");
        }
	}
    testcase TC_Sem_220302_getcall_operation_008() runs on GeneralComp system GeneralComp {
		var GeneralComp v_ptc := GeneralComp.create;
		for(var integer i := 0; i < c_portCount; i := i + 1) {
			connect(self:p[i], v_ptc:p[i]);
			if (i mod 2 > 0) { p[i].call(S:{ p_par := i + 100 }, nowait); }
		}
		v_ptc.start(f());
		v_ptc.done;
	}

    control {
        execute(TC_Sem_220302_getcall_operation_008(), 5000.0);
    }
}