/***************************************************
 ** @author   STF 475
 ** @version  0.0.1
 ** @purpose  9:7.6.2.1, Verify mapping of complex type where both the base and the
 **           extending types have the compositor sequence.
 ** @verdict  pass accept, ttcn3verdict:pass
 ***************************************************/
module Pos_07060201_derived_by_extension_001 {

    import from schema_Pos_07060201_derived_by_extension_001 language "XSD" all;
    //template for EXAMPLE1 and following type system shall be generated from xsd file:
    /*type record E1
    {
        // fields corresponding to attributes of the base and the extending type
        // (in alphabetical order)
        XSD.String birthDateAttrGroup optional,
        XSD.String birthPlaceAttrGroup optional,
        XSD.Integer genderAttrBase optional,
        XSD.String jobPositionAttrGroup optional,
        XSD.String unitOfAge optional,
        // followed by fields corresponding to elements of the base type
        XSD.String titleElemBase,
        XSD.String forenameElemBase,
        XSD.String surnameElemBase,
        // finally fields corresponding to the extending element and group reference
        XSD.Integer ageElemExt,
        G25seq g25seq
    }
    with {
    variant "name as uncapitalized ";
    variant (birthDateAttrGroup, birthPlaceAttrGroup, genderAttrBase, jobPositionAttrGroup,unitOfAge) "attribute";
    };

    type record G25seq {
        XSD.String familyStatusElemInGroup,
        XSD.String spouseElemInGroup optional
    }
    with {
    variant "untagged"
    }*/

    template E1 m_msg := {
      birthDateAttrGroup := omit,
      birthPlaceAttrGroup  := omit,
      genderAttrBase := omit,
      jobPositionAttrGroup  := omit,
      unitOfAge := omit,
      titleElemBase := "titleElemBase1",
      forenameElemBase := "forenameElemBase2",
      surnameElemBase := "surnameElemBase3",
      ageElemExt := 1,
      g25seq :={
          familyStatusElemInGroup :="familyStatusElemInGroup4",
          spouseElemInGroup := omit
      }
    }



    /**
     * @desc The timeout given in seconds after which the test case will be stopped.
     */
    modulepar float PX_TC_EXECUTION_TIMEOUT := 5.0;

    type universal charstring Raw;

    type universal charstring File;
    type record of File FileList;

    type port P message {
        inout all;
    }
    type component C {
        port P p;
    }

    /**
     * @desc lexical compare the charstring p_textToMatch with the contents of the reference XML file and returns true if they represent the same XML structure
     * @param p_textToMatch text to be compared with the UTF-8 contents of the XML file
     * @param p_referenceXmlFile the XML file
     * @param p_xsdFileList the list of XSD files
     * @param p_matchError the error result in case it did not match
     * @param p_referenceTTCN3File the file of the TTCN-3 test module. This path is used to find the reference XML file relative to this path, by keeping the TTCN-3 code file system independent.
     * @return true if p_textToMatch and the contents of p_referenceXmlFile represent the same XML structure
     */
    external function matchFile(Raw p_textToMatch, File p_referenceXmlFile, FileList p_xsdFileList, out universal charstring p_matchError, File p_referenceTTCN3File := __FILE__) return boolean;

    testcase TC_Pos_07060201_derived_by_extension_001() runs on C system C {
        var Raw v_rcv;
        var universal charstring v_matchError;

        map(self:p, system:p);

        // encode the message
        p.send(m_msg);

        alt {
            // compare the encoded message with the reference XML file
            []    p.check(receive(Raw:?) -> value v_rcv) {
                log("XML message ", v_rcv);
                if (matchFile(v_rcv, "Pos_07060201_derived_by_extension_001.xml", { "Pos_07060201_derived_by_extension_001.xsd" }, v_matchError)) {
                    alt {
                        // match decoded value to pass test
                        [] p.receive(m_msg) {
                            setverdict(pass, "Decoded value matches encoded template and reference XML");
                        }
                        [] p.receive {
                            setverdict(fail, "XML decoding failure");
                        }
                    }
                } else {
                    setverdict(fail, v_matchError);
                }
            }
            [] p.receive {
                setverdict(fail, "Raw decoding failure");
            }
        }
    }

    control {
        execute(TC_Pos_07060201_derived_by_extension_001(), PX_TC_EXECUTION_TIMEOUT);
    }

}
