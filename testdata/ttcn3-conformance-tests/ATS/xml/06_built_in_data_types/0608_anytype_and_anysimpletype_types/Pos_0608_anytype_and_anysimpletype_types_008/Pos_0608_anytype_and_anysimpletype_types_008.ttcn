/***************************************************
 ** @author   TTF T014
 ** @version  0.0.1
 ** @purpose  9:6.8, Whitespace handling in anyType with mixed content
 ** @verdict  pass accept, ttcn3verdict:pass
***************************************************/
// The following requirements are tested:
// An element of anyType is able to carry any syntactically valid (well-formed)
// XML content, including mixed content.
// Rule for embed_values from B.3.10 restriction d:
// At the end of decoding, the decoder shall remove all empty strings at the end
// of the embed_values field. As the result of this action, the decoded embed_values
// field either contains no items or its last item is a non empty string.

module Pos_0608_anytype_and_anysimpletype_types_003 {

   import from schema_Pos_0608_anytype_and_anysimpletype_types_003 language "XSD" all;

   template MyType m_msg := {
       embed_values := { "The ordered ", " has arrived. ", ""},
       attr := {"schema:Pos_0608_anytype_and_anysimpletype_types_003 attr=""abc"""},
       elem_list := {
	       "<ns1:foo xmlns:ns1=""schema:Pos_0608_anytype_and_anysimpletype_types_003"">foo</ns1:foo>",
		   "<ns1:bar xmlns:ns1=""schema:Pos_0608_anytype_and_anysimpletype_types_003"">bar</ns1:bar>"
       }
   };

   template MyType m_msg1 modifies m_msg := {
       embed_values := { "The ordered ", " has arrived. "},
   };

    /**
     * @desc The timeout given in seconds after which the test case will be stopped.
     */
    modulepar float PX_TC_EXECUTION_TIMEOUT := 5.0;

    type universal charstring Raw;

    type universal charstring File;
    type record of File FileList;

    type port P message {
        inout all;
    }
    type component C {
        port P p;
    }

    /**
     * @desc lexical compare the charstring p_textToMatch with the contents of the reference XML file and returns true if they represent the same XML structure
     * @param p_textToMatch text to be compared with the UTF-8 contents of the XML file
     * @param p_referenceXmlFile the XML file
     * @param p_xsdFileList the list of XSD files
     * @param p_matchError the error result in case it did not match
     * @param p_referenceTTCN3File the file of the TTCN-3 test module. This path is used to find the reference XML file relative to this path, by keeping the TTCN-3 code file system independent.
     * @return true if p_textToMatch and the contents of p_referenceXmlFile represent the same XML structure
     */
    external function matchFile(Raw p_textToMatch, File p_referenceXmlFile, FileList p_xsdFileList, out universal charstring p_matchError, File p_referenceTTCN3File := __FILE__) return boolean;

    testcase TC_Pos_0608_anytype_and_anysimpletype_types_003() runs on C system C {
        var Raw v_rcv;
        var universal charstring v_matchError;

        map(self:p, system:p);

        // encode the message
        p.send(m_msg);

        alt {
            // compare the encoded message with the reference XML file
            []    p.check(receive(Raw:?) -> value v_rcv) {
                log("XML message ", v_rcv);
                if (matchFile(v_rcv, "Pos_0608_anytype_and_anysimpletype_types_003.xml", { "Pos_0608_anytype_and_anysimpletype_types_003.xsd" }, v_matchError)) {
                    alt {
                        // match decoded value to pass test
                        [] p.receive(m_msg) {
                            setverdict(fail, "Invalid decoding: the last item of the decoded embed_values field cannot contain empty strings");
                        }
						[] p.receive(m_msg1) {
                            setverdict(pass, "Empty strings correctly skipped");
                        }
                        [] p.receive {
                            setverdict(fail, "XML decoding failure");
                        }
                    }
                } else {
                    setverdict(fail, v_matchError);
                }
            }
            [] p.receive {
                setverdict(fail, "Raw decoding failure");
            }
        }
    }

    control {
        execute(TC_Pos_0608_anytype_and_anysimpletype_types_003(), PX_TC_EXECUTION_TIMEOUT);
    }


}
