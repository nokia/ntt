/***************************************************
 ** @author   STF 475, updated by STF 521
 ** @version  0.0.2
 ** @purpose  9:5.2.2, Verify that name clash between module names is resolved using suffix
 ** @verdict  pass accept, ttcn3verdict:pass
***************************************************/
// The following requirements are tested:
// Rule a: If the name being generated is the name of a TTCN-3 module and the character
// string generated by items a) to e) above is identical to an another, previously
// generated TTCN-3 module name, then a postfix shall be appended to the character
// string: the postfix shall consist of a "_" (LOW LINE) followed by the canonical
// lexical representation (see W3C XML Schema Part 2 [9], clause 2.3.1) of an integer,
// unless the name already finishes with a "_" (LOW LINE) character, in which case the
// postfix is an integer only. This integer shall be the least positive integer such
// that the new name is different from all previously generated TTCN 3 modules names
// and clashing definition name.

module Pos_050202_name_conversion_rules_023 {

   import from schema_Pos_050202_name_conversion_rules_023_ language "XSD" all;
   import from schema_Pos_050202_name_conversion_rules_023_1 language "XSD" all;

   template schema_Pos_050202_name_conversion_rules_023_1.MyType m_msg := 1;



    /**
     * @desc The timeout given in seconds after which the test case will be stopped.
     */
    modulepar float PX_TC_EXECUTION_TIMEOUT := 5.0;

    type universal charstring Raw;

    type universal charstring File;
    type record of File FileList;

    type port P message {
        inout all;
    }
    type component C {
        port P p;
    }

    /**
     * @desc lexical compare the charstring p_textToMatch with the contents of the reference XML file and returns true if they represent the same XML structure
     * @param p_textToMatch text to be compared with the UTF-8 contents of the XML file
     * @param p_referenceXmlFile the XML file
     * @param p_xsdFileList the list of XSD files
     * @param p_matchError the error result in case it did not match
     * @param p_referenceTTCN3File the file of the TTCN-3 test module. This path is used to find the reference XML file relative to this path, by keeping the TTCN-3 code file system independent.
     * @return true if p_textToMatch and the contents of p_referenceXmlFile represent the same XML structure
     */
    external function matchFile(Raw p_textToMatch, File p_referenceXmlFile, FileList p_xsdFileList, out universal charstring p_matchError, File p_referenceTTCN3File := __FILE__) return boolean;

    testcase TC_Pos_050202_name_conversion_rules_023() runs on C system C {
        var Raw v_rcv;
        var universal charstring v_matchError;

        map(self:p, system:p);

        // encode the message
        p.send(m_msg);

        alt {
            // compare the encoded message with the reference XML file
            []    p.check(receive(Raw:?) -> value v_rcv) {
                log("XML message ", v_rcv);
                if (matchFile(v_rcv, "Pos_050202_name_conversion_rules_023.xml", { "Pos_050202_name_conversion_rules_023.xsd", "Pos_050202_name_conversion_rules_023_1.xsd" }, v_matchError)) {
                    alt {
                        // match decoded value to pass test
                        [] p.receive(m_msg) {
                            setverdict(pass, "Decoded value matches encoded template and reference XML");
                        }
                        [] p.receive {
                            setverdict(fail, "XML decoding failure");
                        }
                    }
                } else {
                    setverdict(fail, v_matchError);
                }
            }
            [] p.receive {
                setverdict(fail, "Raw decoding failure");
            }
        }
    }

    control {
        execute(TC_Pos_050202_name_conversion_rules_023(), PX_TC_EXECUTION_TIMEOUT);
    }


}
