/***************************************************
 ** @author   STF 475
 ** @version  0.0.1
 ** @purpose  9:5.1.4, Verify that qualified default element form is correctly processed (no namespace prefix)
 ** @verdict  pass accept, ttcn3verdict:pass
***************************************************/
// The following requirements are tested:
// If the TTCN-3 module corresponds to a (present) target namespace and the value of the ...
// elementFormDefault attributes of any schema element information items that contribute to
// the given TTCN-3 module is qualified, the encoding instructions ... "elementFormQualified"
// shall be attached accordingly to the given TTCN-3 module.
//
// Note: as only end-to-end solution is tested, the presence of encoding attributes is not
// checked directly, but through the encoded messages.

module Pos_050104_attributes_of_the_xsd_schema_element_001 {

   import from schema_Pos_050104_attributes_of_the_xsd_schema_element_001 language "XSD" all;

   template MyType m_msg :=
   {
      item1 := "abc",
      item2 := "def"
   };



    /**
     * @desc The timeout given in seconds after which the test case will be stopped.
     */
    modulepar float PX_TC_EXECUTION_TIMEOUT := 5.0;

    type universal charstring Raw;

    type universal charstring File;
    type record of File FileList;

    type port P message {
        inout all;
    }
    type component C {
        port P p;
    }

    /**
     * @desc lexical compare the charstring p_textToMatch with the contents of the reference XML file and returns true if they represent the same XML structure
     * @param p_textToMatch text to be compared with the UTF-8 contents of the XML file
     * @param p_referenceXmlFile the XML file
     * @param p_xsdFileList the list of XSD files
     * @param p_matchError the error result in case it did not match
     * @param p_referenceTTCN3File the file of the TTCN-3 test module. This path is used to find the reference XML file relative to this path, by keeping the TTCN-3 code file system independent.
     * @return true if p_textToMatch and the contents of p_referenceXmlFile represent the same XML structure
     */
    external function matchFile(Raw p_textToMatch, File p_referenceXmlFile, FileList p_xsdFileList, out universal charstring p_matchError, File p_referenceTTCN3File := __FILE__) return boolean;

    testcase TC_Pos_050104_attributes_of_the_xsd_schema_element_001() runs on C system C {
        var Raw v_rcv;
        var universal charstring v_matchError;

        map(self:p, system:p);

        // encode the message
        p.send(m_msg);

        alt {
            // compare the encoded message with the reference XML file
            []    p.check(receive(Raw:?) -> value v_rcv) {
                log("XML message ", v_rcv);
                if (matchFile(v_rcv, "Pos_050104_attributes_of_the_xsd_schema_element_001.xml", { "Pos_050104_attributes_of_the_xsd_schema_element_001.xsd" }, v_matchError)) {
                    alt {
                        // match decoded value to pass test
                        [] p.receive(m_msg) {
                            setverdict(pass, "Decoded value matches encoded template and reference XML");
                        }
                        [] p.receive {
                            setverdict(fail, "XML decoding failure");
                        }
                    }
                } else {
                    setverdict(fail, v_matchError);
                }
            }
            [] p.receive {
                setverdict(fail, "Raw decoding failure");
            }
        }
    }

    control {
        execute(TC_Pos_050104_attributes_of_the_xsd_schema_element_001(), PX_TC_EXECUTION_TIMEOUT);
    }


}
